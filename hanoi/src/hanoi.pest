WHITESPACE = _{ " " | "\n" }

COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }

// LITERALS

nil = { "nil" }

bool = { "true" | "false" }

int = @{ ASCII_DIGIT+ }

char_lit = ${ "'" ~ lit_char ~ "'" }
lit_char = @{
    !("'" | "\\") ~ ANY
  | "\\" ~ ("'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

identifier   = @{ !keyword ~ id_non_digit ~ (id_non_digit | ASCII_DIGIT)* }
id_non_digit = _{ ASCII_ALPHA | "_" }

symbol = { "@" ~ (identifier | string) }

string    = ${ "\"" ~ str_inner ~ "\"" }
str_inner = @{ str_char* }
str_char  =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

literal = { bool | int | char_lit | symbol | nil }

// END LITERALS

builtin = { "#" ~ identifier }

this = _{ "this" }

keyword = _{ "bind" | "match" | "if" | this }

func_call = { "#" ~ identifier ~ "(" ~ (int | identifier) ~ ")" }

delete = { "^" ~ identifier }

copy = { "*" ~ identifier }

expr = { path | literal | func_call | identifier | builtin | delete | copy }

path = { this ~ ("." ~ identifier)* }

sentence      = { sentence_body }
sentence_body = { expr* }

and_then = { sentence ~ ";" ~ code }

if_statement = { sentence ~ "if" ~ "{" ~ code ~ "}" ~ "else" ~ "{" ~ code ~ "}" }

bind = { "bind" ~ identifier ~ ";" ~ code }

match_block = { "match" ~ "(" ~ int ~ ")" ~ "{" ~ match_cases ~ "}" ~ "else" ~ "{" ~ code ~ "}" }

match_cases = { match_case* }
match_case  = { literal ~ "=>" ~ "{" ~ code ~ "}" }

code = { and_then | if_statement | match_block | bind | sentence }

code_decl = { "let" ~ identifier ~ "=" ~ "{" ~ code ~ "}" ~ ";" }

ns_decl = { "mod" ~ identifier ~ "{" ~ namespace ~ "}" }

decl = _{ code_decl | ns_decl | proc_decl }

namespace = { decl* }

file = { SOI ~ namespace ~ EOI }

// Proc syntax

proc_decl = { "proc" ~ identifier ~ proc_args ~ "{" ~ proc_block ~ "}" }

proc_args = {
    "(" ~ ")"
  | "(" ~ identifier ~ ("," ~ identifier)* ~ ")"
}

proc_block      = { proc_statements ~ proc_endpoint }
proc_statements = { proc_statement* }

proc_statement = { proc_let }

proc_let = { "let" ~ proc_args ~ "=" ~ proc_expr ~ ";" }

proc_endpoint = { proc_if | proc_func_call | proc_match_block }

proc_if = {
    "if" ~ identifier ~ "{" ~ proc_block ~ "}" ~ "else" ~ "{" ~ proc_block ~ "}"
}

proc_expr = { literal | proc_func_call }

proc_func_call =  { proc_func_name ~ proc_func_call_args }
proc_func_name = _{ path | identifier }

proc_func_call_args = {
    "(" ~ ")"
  | "(" ~ proc_func_call_arg ~ ("," ~ proc_func_call_arg)* ~ ")"
}

proc_func_call_arg = _{
    literal
  | copy
  | identifier
}

proc_match_block = { "match" ~ proc_expr ~ "{" ~ proc_match_cases ~ "}" }

proc_match_cases = { proc_match_case* }
proc_match_case  = { "(" ~ literal ~ proc_match_bindings_tail ~ ")" ~ "=>" ~ "{" ~ proc_block ~ "}" }

proc_match_bindings_tail = {
    ("," ~ identifier)*
}
