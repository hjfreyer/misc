WHITESPACE = _{ " " | "\n" }

COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }

builtin = { "#" ~ identifier }

this = { "this" }

keyword = _{ "if" | this }

bool = { "true" | "false" }

int = @{ ASCII_DIGIT+ }

identifier   = @{ !keyword ~ id_non_digit ~ (id_non_digit | ASCII_DIGIT)* }
id_non_digit = _{ ASCII_ALPHA | "_" }

func_call = { "#" ~ identifier ~ "(" ~ int ~ ")" }

symbol = { "@" ~ identifier }

literal = {  bool | int }

expr = { literal | func_call | identifier | builtin | this | symbol }

sentence = { expr* }

and_then = { sentence ~ ";" ~ code }

if_statement = { sentence ~ "if" ~ "{" ~ code ~ "}" ~ "else" ~ "{" ~ code ~ "}" }

code = { and_then | if_statement | sentence }

code_decl = { "let" ~ identifier ~ "=" ~ "{" ~ code ~ "}" ~ ";" }

ns_decl = { "mod" ~ identifier ~ "{" ~ namespace ~ "}" }

decl = _{ code_decl | ns_decl }

namespace = { decl* }

file = { SOI ~ namespace ~ EOI }
